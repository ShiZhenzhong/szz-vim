" Toggle rnu and nu
function! ToggleNumber()
    if(&rnu == 1)
        set nornu
        set nonu
    else
        set nu
        set rnu
    endif
endfunc

function! ToggleFoldColumn()
    if(&foldcolumn == 0)
        set foldcolumn=4
    else
        set foldcolumn=0
    endif
endfunc

" init backup directory
function! InitDirectories()
    let root_dir = $HOME . '/' . '.vim'
    let dir_list = {'backup': 'backupdir', 'views': 'viewdir', 'swap': 'directory'}

    for [dirname, setname] in items(dir_list)
        let dir = root_dir . '/' . dirname . '/'
        if exists("*mkdir")
            if !isdirectory(dir)
                call mkdir(dir)
            endif
        endif

        if !isdirectory(dir)
            echo "WARNING: Could not mkdir"
        else
            let dir = substitute(dir, " ", "\\\\ ", "g")
            exec "set " . setname . "=" . dir
        endif
    endfor
endfunction

" Toggle Vexplore with Ctrl-E
function! ToggleVExplorer()
  if exists("t:expl_buf_num")
      let expl_win_num = bufwinnr(t:expl_buf_num)
      if expl_win_num != -1
          let cur_win_nr = winnr()
          exec expl_win_num . 'wincmd w'
          close
          exec cur_win_nr . 'wincmd w'
          unlet t:expl_buf_num
      else
          unlet t:expl_buf_num
      endif
  else
      exec '1wincmd w'
      Vexplore
      let t:expl_buf_num = bufnr("%")
  endif
endfunction

function! ReadJediConfigForJS()
    if g:use_jedi_for_javascript == 1
       let jedi_vim = "~/.vim/bundle/jedi-vim/ftplugin/python/jedi.vim"
       if filereadable(expand(jedi_vim))
           exec ":so " . jedi_vim
       endif
    endif
endfunction

function! CustomizeCR()
    if pumvisible()
        return "\<C-y>"
    else
        return "\<CR>"
    endif
endfunction


